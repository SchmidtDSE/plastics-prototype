###########
## Setup ##
###########

# Build some constants
var purchasingPowerParity = {{ purchasingPowerParity }};
var conversionFactor = 287 / 2125000000;
var recycleSuccess = 0.15;

######################
## Calculate change ##
######################

# Calculate how many points were purchased
var effectiveInvestment = in.{{region}}RecyclingInvestment * purchasingPowerParity * 1000000000;
var potentialRecyclingChangeNoSpillPoints = ((effectiveInvestment * conversionFactor)^0.5);

# Need between 0 and 1
var potentialRecyclingChangeNoSpill = potentialRecyclingChangeNoSpillPoints / 100; 

# Account for recycle success rate, assuming they go to other streams otherwise
var potentialRecyclingChange = potentialRecyclingChangeNoSpill * recycleSuccess;

# Determine what the change could be
var newRecycling = (
    out.{{region}}.eolRecyclingPercent +
    potentialRecyclingChange
);

# Update recycling but keep it in bounds between 0 and 1 (0% to 100%)
limit newRecycling to [0, 1];
var recyclingBeforeChange = out.{{region}}.eolRecyclingPercent;
out.{{region}}.eolRecyclingPercent = newRecycling;

# Determine what the actual change was after bounds
var actualChange = newRecycling - recyclingBeforeChange;

##############################
## Update other EOL streams ##
##############################

# Distribute actualChange across the other EOL streams proportionally to how
# large they are prior to the change
var eolOffset = -1 * actualChange;
distribute eolOffset across [
  out.{{region}}.eolMismanagedPercent,
  out.{{region}}.eolIncinerationPercent,
  out.{{region}}.eolLandfillPercent
] proportionally;

# Ensure percents stay between 0 and 1 (0% to 100%)
limit out.{{region}}.eolMismanagedPercent to [0, 1];
limit out.{{region}}.eolIncinerationPercent to [0, 1];
limit out.{{region}}.eolLandfillPercent to [0, 1];

############################
## Update virgin plastics ##
############################\

# Get total production
var totalProduction = (
  out.{{region}}.inputProduceFiberMT + 
  out.{{region}}.inputProduceResinMT + 
  out.{{region}}.inputAdditivesMT +
  out.{{region}}.inputImportResinMT + 
  out.{{region}}.inputImportArticlesMT + 
  out.{{region}}.inputImportGoodsMT + 
  out.{{region}}.inputImportFiberMT
);

# Assume impact to local production
var localProductionOffset = totalProduction * actualChange * -1;

# Distribute localProductionOffset across local production proportionally to how
# large they are prior to the change
distribute localProductionOffset across [
  out.{{region}}.inputProduceFiberMT,
  out.{{region}}.inputProduceResinMT,
  out.{{region}}.inputAdditivesMT
] proportionally;

# Ensure that those local productions don't go below zero
limit out.{{region}}.inputProduceFiberMT to [0,];
limit out.{{region}}.inputProduceResinMT to [0,];
limit out.{{region}}.inputAdditivesMT to [0,];